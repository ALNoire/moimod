label label_day_3:
    $ persistent.sprite_time = "day"
    $ backdrop = "days" 
    $ new_chapter(2, u"Лето с Женей: День 2") 
    $ renpy.pause(2)
    $ renpy.block_rollback()
    
    scene bg dom with dissolve
    "Надежда на то, что это был просто дурацкий сон не оправдалась."
    "Похоже все и впрямь наяву."
    "С другой стороны, меня никто не прикончил среди ночи, что не может не радовать."
    "Попробую заняться поиском информации."
    "Вожатая говорила про линейку. Кажется, я как раз опаздываю на нее."
    play ambience ambience_ext_road_day fadein 1
    scene bg postr with dissolve
    "Плотные линии пионеров на площади действительно уже построились."
    "Я взглядом разыскал свой отряд и присоединился к остальным."
    $ renpy.pause(2)
    scene bg ext_square_day
    show mt normal panama pioneer far at center
    with dissolve
    mt "Все свои обязанности помнят, я надеюсь."
    mt "А вот и наш нерадивый пионер. С добрым утром. Тебя снова все ждут."
    ya "Извиняюсь, задержался."
    show mt normal panama pioneer close at center with dissolve
    mt "Надо же, извиняется. Совесть проснулась после вчерашнего?"
    ya "Вроде того."
    mt "А теперь слушай указания. Поскольку ты новенький, возьми обходной лист."
    mt "Сходишь с ним и отметишься в каждом клубе, а так же заведешь читательский билет в библиотеке."
    mt "Вечером сдашь мне в руки, понял?"
    ya "Все понял."
    mt "Вот и хорошо. А теперь за дело."
    "Пионеры разошлись по площади, оставив меня одного."
    "Как раз собирался идти в библиотеку, а теперь у меня для этого только больше причин стало."
    "Самое время познакомиться с этой самой Женей, которую даже Алиса побаивалась." 
    "Уже самому интересно кто это такая."
    scene bg ext_library_day with dissolve
    "Завтрак я намеренно пропустил. Не терпелось заняться изучением вопроса."
    "Из-за двери доносились громкие крики. Очевидно, кто-то устроил скандал."
    "Я пошел ко входу, не задерживаясь."
    "Уж очень хотелось посмотреть что происходит." 
    play sound sfx_open_door_1
    play ambience ambience_library_day fadein 1
    scene bg int_library_day
    show zh rage glasses pioneer far at center
    show us sad pioneer far at left
    with dissolve
    mz "А теперь пошла вон отсюда и без книги чтобы я тебя не видела!"
    show us sad pioneer far at left
    us "Где ж я теперь ее найду-то?"
    mz "Там же где и потеряла. Все! Уходи!"
    "Так вот какая она - библиотекарша."
    "На вид маленькая и безобидная девушка в очках."
    "Ростом полтора метра, но суровости ей не занимать."
    "Едва до слез мелкую не довела."
    hide us with dissolve
    play sound sfx_open_door_1
    show zh normal glasses pioneer far
    mz "Чего тебе, пионер?"
    ya "Да вот... Обходной подписать. И еще книг каких-нибудь по истории."
    mz "Вот это запросы у тебя. Школьной программы не хватило?"
    ya "Освежить память не помешает."
    mz "Ладно, заполни читательский билет, а я пока посмотрю что у нас из исторической литературы имеется."
    hide zh with dissolve
    "Я подошел к столику и принялся заполнять документ, как мне на глаза попалась любопытная тетрадь, лежавшая на краю."
    "Вероятно, это какие-то записи Жени."
    "Меня начало одолевать любопытство. Что же там? Личный дневник? Черная бухгалтерия? Журнал посещений?"
    menu:
        "Подглядеть в тетрадку":
            "Наверняка там что-то интересное."
            "Я просто не смог удержаться, чтобы не взглянуть одним глазком, пока Женя не видит."
            $ set_mode_nvl()
            "И тут их бравый капитан отдал приказ на отход. Космический крейсер 'Союз' сорвался с орбиты планеты Гиперион-7. Навстречу новым опасностям, приключениям и козням врагов. Капитан не сомневался ни в ком из своего героического экипажа, с которыми он прошел огонь воду и бессчисленное количество сражений."
            $ set_mode_adv()
            nvl clear
            "Советская космоопера. Оригинально. Оказывается, она довольно творческая личность."
            play sound sfx_punch_medium
            with hpunch
            "Я почувствовал ощутимый удар по голове тяжелым предметом. Вероятно, книгой."
            show zh angry glasses pioneer close with dspr
            mz "Ах ты хунта!"
            mz "Ах ты злодей!"
            mz "Тебе кто разрешал чужие вещи трогать, негодяй?!"
            ya "Извини, просто стало интересно что ты пишешь."
            show zh rage glasses pioneer close
            mz "Ишь чего удумал, интересно ему. Забирай свои книги и уходи, пока я тебя ими же и не прибила!"
            "Я поспешил сделать как она просит. Хоть Женя и маленькая, но в том, что может прибить я не сомневался."
            $ prosmotr = True
        "Не смотреть":
            "Думаю, не стоит лезть в ее частную жизнь."
            "Действительно, может это какие-то личные записи о которых мне не нужно знать."
            show zh normal glasses pioneer close with dissolve
            mz "Вот твои книги, как ты и просил."
            "Она выложила на стол несколько книг, начиная от древней истории, заканчивая эпохой советского периода."
            mz "Не знаю что тебя интересует, принесла все, чтобы дважды не ходить."
            mz "Можешь все забрать, если хочешь. Иди, у меня тут еще дела, знаешь ли."

    stop ambience
    play sound sfx_open_door_2
    $ renpy.block_rollback()
    play ambience ambience_ext_road_day fadein 1
    scene bg ext_library_day with dissolve
    "Теперь надо решить куда где я проведу следующие несколько часов за изучением книг."
    $ disable_all_zones()
    $ set_zone("estrade","estrada_2")
    $ set_zone("sport_area","sport_2")
    $ show_map()

label estrada_2:
    play ambience ambience_ext_road_day fadein 1
    scene bg ext_stage_normal_day with dissolve
    "Деревянная сцена перед которой расположился косой ряд стульев."
    "Не худший вариант для уединенного чтения."
    "И похоже не я один так считал."
    "На краю сцены, свесив ноги к земле, сидела Лена, держа в руках книгу в цветной обложке."
    "Она осторожно посматривала на меня, но смущенно отводила взгляд, всякий раз, когда я отвлекался от чтения и смотрел в ее сторону."
    "Будто стеснялась заговорить, но очень хотела этого."
    ya "Тоже книгу читаешь?"
    show un normal pioneer far at left with dissolve
    un "Как раз дочитала."
    "Ее тихий, спокойный голос вызывал умиротворение."
    ya "Я вот тоже. Правда историю, а не художественную литературу."
    un "Готовишься к поступлению?"
    ya "Да, вроде того."
    show un smile pioneer far
    un "Желаю успехов. Ты прости, мне идти пора, приятно было поговорить."
    ya "И мне."
    hide un with dissolve
    "Лена ушла в сторону площади."
    "Теперь никто не отвлекал меня от изучения материала."
    "Как бы я ни старался, не мог найти никаких несоответствий. Ни в древней истории, ни в современной советской."
    "В некоторых моментах все же закрадывались сомнения, но моих знаний истории было недостаточно, чтобы помнить как оно было в моем мире и времени."
    "Грубо говоря, этот мир почти идентичен прошлому из моей реальности."
    "Даже если какие-то отличия и были, то слишком незначительные, чтобы поменять ход вещей."
    "Очередная моя идея закончилась неудачей."
    "Я так и не смог узнать ничего важного или нового об этом месте."  
    stop ambience
    jump day_2_2

label sport_2:
    stop ambience fadeout 1
    play ambience ambience_day_countryside_ambience fadein 1
    scene bg ext_playground_day with dissolve
    "Я уселся на трибуну напротив спортивной площадки."
    "Не лучшее место для чтения, но зато свежий воздух и выкрики спортсменов не дадут заскучать под этой макулатурой."
    "Краем глаза заметил лежавшую под скамейкой книгу."
    "{b}Сборник советский сказок{/b}. Автор не указан. Ахинея небось какая-нибудь."
    "Наверное именно ее потеряла Ульяна, не выдержав соблазна побеситься на футбольном поле, а потом и вовсе забыла."
    "Нужно будет отдать книгу в библиотеку."
    "Я сложил сборник под остальные, попутно открывая первую книгу по древней истории."
    "Как бы я ни старался, не мог найти никаких несоответствий. Ни в древней истории, ни в современной советской."
    "В некоторых моментах все же закрадывались сомнения, но моих знаний истории было недостаточно, чтобы помнить как оно было в моем мире и времени."
    "Грубо говоря, этот мир почти идентичен прошлому из моей реальности."
    "Даже если какие-то отличия и были, то слишком незначительные, чтобы поменять ход вещей."
    "Очередная моя идея закончилась неудачей."
    "Я так и не смог узнать ничего важного или нового об этом месте."
    play sound sfx_punch_medium
    with hpunch
    if prosmotr == True:
        "Да что за день сегодня такой? Каждый норовит дать мне по голове."
    else: 
        "Ай, какого черта?"

    "Футбольный мяч не без помощи Ульяны попал прямо в меня."
    show us normal sport close at left with dspr
    us "А вот нечего тут книжки читать, это спортивная площадка, а не литературный клуб."
    us "Что ты там читаешь, кстати? Ерунду какую-то небось."
    ya "То есть я еще и виноват?"
    ya "Да и кто бы говорил. Я тут нашел твою книгу, которую ты вчера потеряла."
    us "Так это ты ее забрал? Отдай немедленно!"
    show us smile sport close
    ya "Я ее под трибуной нашел, а доверять тебе книги с таким отношением я не стану. Сам завтра отнесу."
    us "Точно отнесешь? А то Женя с меня кожу сдерет заживо, если книгу не сдам."
    show us smile sport close at center
    ya "При условии, что ты перестанешь отвлекать меня от дел."
    us "Ой, тоже мне занятой нашелся."
    us "Ладно, пойду с ребятами в футбол доигрывать."
    hide us with dissolve
    stop ambience
    $ kniga = True
    jump day_2_2

label day_2_2:
    play ambience ambience_ext_road_day fadein 1
    scene bg ext_square_day with dissolve
    "Я вернулся на площадь."
    "Уже через несколько минут после этого прозвучал сбор на обед."
    "Пропущенный завтрак давал о себе знать."
    "Я снова испытывал сильный голод." 
    "Может, на природе так аппетит разыгрывается?"
    "Ведь дома, в своем времени я мог обходиться без еды куда дольше, не ощущая такого дискомфорта."

    show black with dissolve
    stop ambience fadeout 1
    $ renpy.pause (1, hard = True)
    play ambience ambience_dining_hall_full fadein 1
    scene bg int_dining_hall_people_day
    hide black 
    with dissolve

    "Ароматный запах горячей пищи окончательно свел меня с ума."
    "Не в силах ждать, я проскочил без очереди, за обедом."
    "Алиса тоже подсуетилась."
    "Забрала свою порцию, минуя длинный ряд пионеров, сделав вид, что мы вместе."
    "Недовольные пионеры лишь разводили руками, переговариваясь между собой, но никто не подошел спросить с меня за это." 
    "С Алисы тем более."
    "Рыжеволосая подсела ко мне, не спрашивая разрешения."
    show dv laugh pioneer2 far at center with dissolve
    dv "Ловко ты их, еще так уверенно."
    dv "Мне нравится."
    show dv laugh pioneer2 close with dissolve
    ya "Просто кушать очень хотелось."
    dv "Да не волнуйся ты. Я человек надежный, в обиду не дам тебя."
    dv "Надеюсь, и ты меня прикроешь, если что."
    ya "Звучит так, будто ты что-то задумала." 
    dv "Ничего такого..."
    show dv smile pioneer2 close
    "Алиса явно недоговарива."
    ya "Если хочешь заставить меня участвовать в чем-то, лучше сразу говори."
    ya "Не люблю неприятных сюрпризов."
    dv "Надо, чтобы ты кое что подержал у себя до вечера."
    "Она подсела ближе и неглядя, плавными движениями рук положила мне в карман помятую пачку сигарет."
    ya "Вот как. Не расскажешь где взяла?"
    dv "Какая разница. Из деревни принесла. Так что? Спрячешь у себя? Кто-то из пионеров сдал меня Ольге Дмитриевне и та теперь собирается устроить в нашем домике обыск."
    ya "Спрячу, если расскажешь про эту деревню."
    dv "Я не больше твоего знаю. Сама туда не ходила ни разу."
    show dv normal pioneer2 close
    dv "Ребята из другого отряда таскают иногда, я с ними в доле."
    dv "А что, тоже хочешь?"
    ya "Не курю."
    dv "Ну и отлично, значит сколько дала, столько вернешь к вечеру."
    show dv normal pioneer2 far at right with dissolve
    ya "Ладно."
    hide dv with dissolve
    "Алиса ушла, оставив меня одного за столиком."
    "Значит, тут есть какая-то деревня. Интересно, кто мог бы знать о ней больше?"
    "К вожатой обращаться смысла нет, она все равно не пустит."
    "Пионеры тоже знают не много."
    "Стоит расспросить парней в клубе кибернетики. Все равно обходной надо подписать." 
    "Но перед этим загляну к Мику."
    "Нет никакого желания оставлять общение с ней до позднего вечера."
    stop ambience
    play sound sfx_open_door_1
    play ambience ambience_music_club_day fadein 1
    scene bg int_musclub_day
    show mi normal pioneer far 
    with dissolve
    mi "О, решил все таки записаться в музыкальный клуб?"
    "Она смотрела на меня с некоторой надеждой."
    "Видимо, желающих было не очень много."
    ya "Пока только обходной подписать. Честно, музыкант из меня никакой."
    mi "Все проще, чем кажется. Показать как я играю?"
    ya "В другой раз. Мне еще к кибернетикам на подпись идти."
    show mi sad pioneer far
    mi "Жаль, но ничего не поделаешь. Давай подпишу."
    "Мику погрустнела."
    ya "Не отчаивайся. Мало ли юных дарований в этом лагере."
    play sound sfx_open_door_1
    stop ambience
    play ambience ambience_ext_road_evening fadein 1
    scene bg ext_musclub_day with dissolve
    "Да, не очень вежливо получилось."
    "И какого черта я вообще занимаюсь всей этой ерундой?"
    "Я никакой не пионер!"
    "С другой стороны, осталось одну закорючку поставить и буду свободен."
    "Теперь пора в клуб к парням."
    "Двусмысленно..."
    stop ambience fadeout 1
    scene bg int_clubs_male_sunset
    show sh smile pioneer far at center
    show el smile pioneer far at left
    with dissolve
    ya "Привет, ребята. Подпишите обходной."
    sh "О, привет. Не хочешь в наш клуб записаться? Мы тут всякие штуки интересные мастерим, нам еще одни руки ох как нужны."
    el "Это точно, сам посмотри, вдвоем уже не справляемся."
    ya "Нет, с клубами я давно завязал."
    sh "Уверен? Подумай хорошенько."
    ya "Сказал же, что не хочу. Вы лучше мне про деревню расскажите."
    sh "Про деревню? А чего про нее рассказывать-то. Есть тут какая-то деревня. Как и все деревни."
    el "Да, туда ребята иногда за сигаретами бегают. Нет, не мы. Мы таким не занимаемся, да Шурик?"
    show el laugh pioneer far
    show sh serious pioneer far
    sh "Д-да..."
    el "Вот, держи свой обходной."
    play sound sfx_open_door_1
    play ambience ambience_ext_road_evening fadein 1
    scene bg ext_square_sunset with dissolve
    "Думаю, они не врали."
    "Изначально идея была глупой."
    "Даже если я доберусь до этой деревни, то что с того? Сельские жители отправят меня обратно в мое время?"
    "Тут хотя бы кормят. Бежать не имеет смысла."
    "По логике, дело может проясниться под конец смены, когда автобус заберет пионеров. Может, на нем я и попаду обратно?"
    "А пока буду наслаждаться. Не каждый день такое приключение. В моей-то жизни."
    show pi far at center with dspr
    pi "Ты наконец понял?"
    pi "Все дело в автобусе."
    ya "Ты кто такой вообще?"
    show pi close at center
    pi "Всего лишь невольный участник этого спектакля. Как и ты."
    ya "Так ты тоже из реального мира?"
    pi "Теперь это твоя реальность."
    show pi smile close at center
    pi "Они не знают, только мы."
    pi "Будь осторожен. Это место только кажется безобидным."
    hide pi with dissolve
    "Что? Куда он делся? Только что же был тут?"
    "Я оглянулся, но никого похожего на этого странного пионера поблизости не было."
    "Пора бы уже привыкнуть к происходящему."
    show sl normal pioneer far at right with dissolve
    sl "Пора на ужин, не опаздывай."
    ya "Уже иду."

    show black with dissolve
    stop ambience fadeout 1
    hide sl with dissolve
    $ renpy.pause (1, hard = True)
    play ambience ambience_music_club_day fadein 1
    scene bg int_dining_hall_night
    hide black 
    with dissolve

    $ persistent.sprite_time = "night"
    "Ужин прошел без происшествий." 
    "Сразу после того, как большинство пионеров покинуло лагерь, Шурик и Электроник собрали отряд в пустом зале."
    "Я тоже присоединился. Было интересно по какому поводу сбор."
    show sh normal pioneer far at center with dissolve
    show el normal pioneer far at right with dissolve
    show dv normal pioneer far at left with dissolve
    show mi normal pioneer far at fleft with dissolve
    show sl normal pioneer far at cleft with dissolve
    show un normal pioneer far at cright with dissolve
    show us normal pioneer far at fright with dissolve
    ya "Что тут происходит?"
    show sh smile pioneer far at center
    sh "Мы будем играть карточный турнир, только Жени не хватает."
    ya "Так нас же вроде четное количество."
    el "Я буду ведущим, так что нам не хватает одного человека."
    show el smile pioneer far at right with dissolve
    ya "Пойду разыщу Женю и попробую позвать. Если пропаду, можете начинать без нас."
    "Вытащить ее к остальным будет той еще задачей."
    stop ambience fadeout 1
    play ambience ambience_ext_road_night fadein 1
    scene bg ext_square_night with dissolve
    "Найти ее оказалось нетрудно."
    "Женя сидела на скамейке перед памятником в полном одиночестве."
    show zh normal glasses pioneer close at center with dissolve
    ya "Привет Женя. Почему ты здесь, а не со всеми?"

    if prosmotr == True:
        show zh angry glasses pioneer close at center
        mz "Посмотрите кто пришел. Любитель заглядывать в чужие тетради."
        ya "Извини еще раз, я лишь хотел узнать о чем ты пишешь."
        show zh normal glasses pioneer close at center
        mz "Понравилось?"
        ya "Я почти ничего и прочитать не успел до того, как ты меня книгой по голове ударила. Больно, между прочим."
        mz "Это чтоб неповадно было."
        ya "Ну хватит тебе, я же извинился."
        show zh bukal glasses pioneer close
        mz "И что с того?"
        ya "Как вариант, перестать обижаться из-за этого."
        mz "Вот еще!"
        ya "Ну что? Идем к остальным?"
        mz "Я никуда не пойду, мне не интересно играть в дурацкие азартные игры."
        ya "Почему?"
        jump night_2
    else:
        mz "А почему должна быть?"
        ya "Тебя все ждут. Настоящий пионер не может подвести товарищей."
        show zh bukal glasses pioneer close
        mz "Все равно не пойду."
        show zh normal glasses pioneer close at center
        ya "Почему?"
        jump night_2

label night_2:
    $ renpy.block_rollback()
    mz "Не хочу."
    ya "Да, уж. Не любишь же ты людей."
    mz "А вот и неправда."
    show zh shyangry glasses pioneer close
    mz "Дураков не люблю и подлиз всяких."
    ya "К какому типу меня отнесешь?"
    show zh shy glasses pioneer close
    mz "Пока не решила."
    ya "Тогда и я никуда не пойду. Будем сидеть тут вместе."
    mz "А ты почему?."
    show zh confused glasses pioneer close
    ya "Надоело, в тишине посидеть хочется. На звезды посмотреть, ощутить дуновение ночного ветра."
    mz "Получается, вместе не идем?"
    ya "Я ребят предупредил, чтобы начинали без нас, если вдруг не получится тебя уговорить."
    mz "Не сильно ты и старался."
    show zh normal glasses pioneer close
    ya "Может мне нужен был повод, чтобы сбежать, дабы посидеть с одной библиотекаршей."
    show zh smile glasses pioneer close 
    mz "Подлиза."
    ya "Может, все же дурак?"
    mz "Еще не решила."
    "Несмотря на все произошедшее, меня необъяснимым образом стало тянуть к Жене. Суровая, нелюдимая и порой даже слишком вспыльчивая."
    hide zh with dissolve
    "Но было в ней и что-то притягательное. Нечто особенное, за толстым защитным слоем она была романтичной и милой девушкой. Не каждый хочет и может это в ней рассмотреть."
    "Мы сидели так до глубокой ночи. В тишине, слушая лишь шум ветра и глядя в ночное небо." 
    "Ни один из нас больше не проронил ни слова, но это молчание было куда важнее и дороже тысячи фраз."
    "Меня омрачал лишь тот факт, что нас не оставят в покое надолго. Максимум пара часов и весь лагерь поднимется на наши поиски."
    "А значит, надо было собираться уходить."
    show zh normal glasses pioneer far with dissolve
    ya "Женя, тебя проводить?"
    mz "Не надо. Хорошо посидели. Наконец-то хоть кто-то понимает всю прелесть тишины и одиночества."
    ya "В таком случае, не настаиваю. Увидимся завтра."
    hide zh with dissolve

    if kniga == True:
        show zh normal glasses pioneer far at right
        ya "Подожди, Женя."
        mz "Чего еще?"
        ya "Я нашел книгу, которую Ульяна потеряла. Завтра занесу в библиотеку, если ты не против."
        mz "Приходи, буду ждать."
        jump night_2_2
    else:
        jump night_2_2

label night_2_2:
    hide zh with dissolve
    "Мне тоже стоило идти."
    "Такими темпами я просплю утреннее построение."
    stop ambience
    play sound sfx_open_door_1
    scene bg dom with dspr
    "День был долгим и трудным. Неизвестно, что ждет меня завтра, так что лучше хорошенько отдохнуть."
    show blink
    $renpy.pause (2)
    jump label_day_4